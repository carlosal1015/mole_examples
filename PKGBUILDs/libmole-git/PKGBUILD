_base=mole
pkgname=lib${_base}-git
pkgdesc="The Mimetic Operators Library Enhanced"
pkgver=1.0.r22.gb10acea
pkgrel=1
arch=(x86_64)
url="https://github.com/csrc-sdsu/${_base}"
license=(GPL-3.0-or-later)
depends=(armadillo)
makedepends=(doxygen eigen graphviz openblas)
optdepends=('matlab: for MATLAB support'
  'octave: for Octave support'
  'eigen: for the sparse LU factorization')
source=(git+${url}.git#branch=master)
sha512sums=('SKIP')
provides=(${_base})
conflicts=(${_base})

pkgver() {
  cd ${_base}
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_base}
  make SHARED_LIB=1 EIGEN=1 ARMA=1
  doxygen
}

package() {
  cd ${_base}
  install -Dm 755 mole_C++/lib${_base}.so -t "${pkgdir}/usr/lib"
  make clean

  install -d ${pkgdir}/usr/include/${_base}
  install mole_C++/*.h ${pkgdir}/usr/include/${_base}

  install -d "$pkgdir/usr/share/${_base}"
  mv mole_MATLAB ${pkgdir}/usr/share/${_base}

  # install docs
  install -d ${pkgdir}/usr/share/doc/${_base}
  mv doc_C++ ${pkgdir}/usr/share/doc/${_base}
  mv doc_MATLAB ${pkgdir}/usr/share/doc/${_base}

  # install tutorials
  install -d ${pkgdir}/usr/share/doc/${_base}/examples
  mv examples_C++ ${pkgdir}/usr/share/doc/${_base}/examples
  mv examples_MATLAB ${pkgdir}/usr/share/doc/${_base}/examples

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
