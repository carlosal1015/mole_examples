# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
_base=mole
pkgname=lib${_base}-git
pkgdesc="The Mimetic Operators Library Enhanced"
pkgver=1.0.r373.g732d57a
pkgrel=1
arch=(x86_64)
url="https://github.com/csrc-sdsu/${_base}"
license=(GPL-3.0-only)
depends=(armadillo octave superlu)
makedepends=(cmake eigen3 git)
  # doxygen graphviz inkscape texlive-binextra texlive-luatex texlive-latexrecommended texlive-fontsrecommended ttf-dejavu texlive-latexextra
  # python-setuptools python-sphinx-book-theme python-sphinx_design python-sphinx-copybutton python-sphinx-togglebutton
  # python-docutils python-pillow python-breathe python-myst-parser python-sphinx_rtd_theme
  # python-sphinxcontrib-bibtex python-sphinxcontrib-katex python-standard-imghdr python-altair python-sphinx-hoverxref
  # python-sphinxcontrib-serializinghtml python-sphinxcontrib-htmlhelp
  # python-sphinxcontrib-matlabdomain
checkdepends=(gtest)
optdepends=('matlab: for MATLAB support'
  'octave: for Octave support'
  'eigen: for the sparse LU factorization')
source=(git+${url}.git#branch=master
  git+https://github.com/gllmflndn/m2html.git#branch=main)
sha512sums=('SKIP'
            'SKIP')
provides=(${_base})
conflicts=(${_base})

pkgver() {
  cd ${_base}
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  # Similar issue https://lists.altlinux.org/pipermail/sisyphus-cybertalk/2025-May/126338.html
  sed -i 's/^set(CMAKE_CXX_STANDARD 14/set(CMAKE_CXX_STANDARD 17/' ${_base}/CMakeLists.txt
  sed -i '/^if(U/,+6 s/^/#/' ${_base}/CMakeLists.txt
  sed -i '/^set(ARMADILLO_VERSION/,+68 s/^/#/' ${_base}/CMakeLists.txt
  sed -i '/^set(LINK_LIBS/,+3 s/^/#/' ${_base}/CMakeLists.txt
  sed -i '118i set(LINK_LIBS ${ARMADILLO_LIBRARIES} ${OpenBLAS_LIBRARIES} ${SUPERLU_INSTALL_DIR}/lib/libsuperlu.so ${LAPACK_LIBRARY})' ${_base}/CMakeLists.txt # ${EIGEN3_LIBRARIES}
  sed -i '/^include(/,+9 s/^/#/' ${_base}/tests/cpp/CMakeLists.txt
  sed -i 's/^	@python/	#@python/' ${_base}/doc/sphinx/Makefile
  mv m2html ${_base}
  mkdir -p ${_base}/doc/doxygen/cpp
  # exit 0
  # sed -i 's/svg/pdf/' ${_base}/README.md
  # mkdir -p ${_base}/doc/sphinx/source/api/intros/doc/assets/img
  # cd ${_base}/doc/assets/img
  # inkscape joss-status.svg --export-area-drawing --batch-process --export-type=pdf --export-filename=joss-status.pdf
  # inkscape matlab-file-exchange.svg --export-area-drawing --batch-process --export-type=pdf --export-filename=matlab-file-exchange.pdf
  # cp ${srcdir}/${_base}/doc/assets/img/*.pdf ${_base}/doc/sphinx/source/api/intros/doc/assets/img
}

build() {
  cmake \
    -S ${_base} \
    -B build \
    -DBUILD_SHARED_LIBS=TRUE \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_STANDARD=17 \
    -DCMAKE_CXX_COMPILER=g++ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build --target mole_C++

  cd ${_base}
  octave -q -p m2html --eval "m2html('mfiles', 'src/matlab_octave', 'htmldir', 'doc/doxygen/matlab_octave', 'global', 'on', 'template', 'blue')"
  # cd doc/sphinx/
  # make doc-doxygen doc-html doc-latexpdf
  # cd build/latex
  # make
}

check() {
  cmake --build build --target run_tests
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install
  install -d ${pkgdir}/usr/include/${_base}
  install ${_base}/src/cpp/*.h ${pkgdir}/usr/include/${_base}

  install -d "${pkgdir}/usr/share/${_base}"
  mv ${_base}/src/matlab_octave ${pkgdir}/usr/share/${_base}

  # install docs
  install -d ${pkgdir}/usr/share/doc/${_base}
  # mv ${_base}/doc/sphinx/build/html ${pkgdir}/usr/share/doc/${_base}
  # mv ${_base}/doc/sphinx/build/latex/PDF/MOLE-docs.pdf ${pkgdir}/usr/share/doc/${_base}
  mv ${_base}/doc/doxygen ${pkgdir}/usr/share/doc/${_base}

  # install tutorials
  install -d ${pkgdir}/usr/share/doc/${_base}/examples
  mv ${_base}/examples/cpp ${pkgdir}/usr/share/doc/${_base}/examples
  mv ${_base}/examples/matlab_octave ${pkgdir}/usr/share/doc/${_base}/examples

  install -Dm 644 ${_base}/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
