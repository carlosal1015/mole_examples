# Adapted from the package petsc with the following original contributors:
# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>
_base=mole
pkgname=lib${_base}-git
pkgdesc="The Mimetic Operators Library Enhanced"
pkgver=1.0.r130.g16d23bb
pkgrel=1
arch=(x86_64)
url="https://github.com/csrc-sdsu/${_base}"
license=(GPL-3.0-only)
depends=(armadillo)
makedepends=(cmake doxygen eigen graphviz openblas git) # python-sphinx texlive-bin
# checkdepends=(gtest)
optdepends=('matlab: for MATLAB support'
  'octave: for Octave support'
  'eigen: for the sparse LU factorization')
source=(git+${url}.git#branch=master)
sha512sums=('SKIP')
provides=(${_base})
conflicts=(${_base})

pkgver() {
  cd ${_base}
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  sed -i 's/SUBDIRS = src\/cpp examples\/cpp tests\/cpp/SUBDIRS = src\/cpp examples\/cpp/' ${_base}/Makefile
}

build() {
  cd ${_base}
  make SHARED_LIB=1 EIGEN=1 ARMA=1
  doxygen
}

package() {
  cd ${_base}
  install -Dm 755 src/cpp/lib${_base}.so -t "${pkgdir}/usr/lib"
  make clean

  install -d ${pkgdir}/usr/include/${_base}
  install src/cpp/*.h ${pkgdir}/usr/include/${_base}

  install -d "${pkgdir}/usr/share/${_base}"
  mv src/matlab ${pkgdir}/usr/share/${_base}

  # install docs
  install -d ${pkgdir}/usr/share/doc/${_base}
  mv doc/api_docs/cpp ${pkgdir}/usr/share/doc/${_base}
  mv doc/api_docs/matlab ${pkgdir}/usr/share/doc/${_base}

  # install tutorials
  install -d ${pkgdir}/usr/share/doc/${_base}/examples
  mv examples/cpp ${pkgdir}/usr/share/doc/${_base}/examples
  mv examples/matlab ${pkgdir}/usr/share/doc/${_base}/examples

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
