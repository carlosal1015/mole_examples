_base=mole
pkgname=${_base}-git
pkgdesc="The Mimetic Operators Library Enhanced"
pkgver=2.0.r44.g8b0ebcc
pkgrel=1
arch=(x86_64)
url="https://github.com/csrc-sdsu/${_base}"
license=(GPL-3.0-or-later)
depends=(armadillo)
makedepends=(cmake doxygen graphviz eigen)
optdepends=('matlab: for MATLAB support'
  'octave: for Octave support'
  'eigen: for the sparse LU factorization')
source=(git+${url}.git#branch=master
  https://raw.githubusercontent.com/carlosal1015/mole_examples/main/patches/includes.patch)
sha512sums=('SKIP'
  'e7ad19476aef8b0f0a3cd3505c3d4546b0e1b238e5772e4aa6060ac990f8dfa3e29a44c3a6069110194f25c64e1c188cdf98c2b587f160043b9615e3124f94ea')
provides=(${_base})
conflicts=(${_base})

pkgver() {
  cd ${_base}
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${_base}
  patch -p1 -i ../includes.patch
}

build() {
  cd ${_base}
  make SHARED_LIB=1 EIGEN=1
  doxygen
}

package() {
  cd ${_base}
  install -Dm 755 mole_C++/libmole.so -t "${pkgdir}/usr/lib"
  make clean

  install -d ${pkgdir}/usr/include/${_base}
  install mole_C++/*.h ${pkgdir}/usr/include/${_base}

  install -d "$pkgdir/usr/share/${_base}"
  mv mole_MATLAB ${pkgdir}/usr/share/${_base}

  # install docs
  install -d ${pkgdir}/usr/share/doc/${_base}
  mv doc_C++ ${pkgdir}/usr/share/doc/${_base}
  mv doc_MATLAB ${pkgdir}/usr/share/doc/${_base}

  # install tutorials
  install -d ${pkgdir}/usr/share/doc/${_base}/examples
  mv examples_C++ ${pkgdir}/usr/share/doc/${_base}/examples
  mv examples_MATLAB ${pkgdir}/usr/share/doc/${_base}/examples

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
