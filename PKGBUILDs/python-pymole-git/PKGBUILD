# Maintainer: Carlos Aznarán <caznaranl@uni.pe>
_base=pymole
pkgname=python-${_base}-git
pkgdesc="Mimetic Methods for Python"
pkgver=0.0.post14+g9d0eddb
pkgrel=1
arch=(any)
url="https://github.com/carlosal1015/${_base}"
license=(GPL-3.0-or-later)
depends=(python-scipy)
makedepends=(python-build python-installer python-setuptools-scm python-wheel python-nbsphinx
  python-sphinx_rtd_theme python-nbsphinx-link python-sphinx_mdinclude pandoc-cli git)
checkdepends=(python-pytest)
source=(git+${url}.git?signed#branch=main)
validpgpkeys=('3880BDA725DE92E7BE7C1A2F6225FD2615EB3DEE') # Carlos Aznarán <caznaranl@uni.pe>
sha512sums=('SKIP')
provides=("python-${_base}")
conflicts=("python-${_base}")

pkgver() {
  cd ${_base}
  python -m setuptools_scm
}

build() {
  cd ${_base}
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
  cd doc
  make html
}

check() {
  cd ${_base}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest tests
}

package() {
  cd ${_base}
  PYTHONPYCACHEPREFIX="${PWD}/.cache/cpython/" python -m installer --destdir="${pkgdir}" dist/*.whl
  install -d ${pkgdir}/usr/share/doc/${pkgname}/sphinx
  mv doc/build/html "${pkgdir}/usr/share/doc/${pkgname}/sphinx"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
