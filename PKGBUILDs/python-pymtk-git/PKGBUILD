# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
_base=pymtk
pkgname=python-${_base}-git
pkgdesc="Mimetic Methods for Python"
pkgver=r32.6e921e8
pkgrel=1
arch=(any)
url="https://github.com/igormorgado/${_base}"
license=(MIT)
depends=(python-scipy python-sympy)
makedepends=(python-build python-installer python-setuptools python-wheel python-sphinx git)
checkdepends=(python-pytest python-ipdb)
source=(git+${url}.git#branch=master)
sha512sums=('SKIP')

pkgver() {
  cd ${_base}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  sed -i 's/__version__/version__/' ${_base}/docs/conf.py
  sed -i 's/float_/float/' ${_base}/${_base}/${_base}.py
}

build() {
  cd ${_base}
  python -m build --wheel --skip-dependency-check --no-isolation
  cd docs
  make html
}

check() {
  cd ${_base}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest tests
}

package() {
  cd ${_base}
  PYTHONPYCACHEPREFIX="${PWD}/.cache/cpython/" python -m installer --destdir="${pkgdir}" dist/*.whl
  install -d ${pkgdir}/usr/share/doc/${pkgname}/sphinx
  mv docs/_build/html "${pkgdir}/usr/share/doc/${pkgname}/sphinx"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
